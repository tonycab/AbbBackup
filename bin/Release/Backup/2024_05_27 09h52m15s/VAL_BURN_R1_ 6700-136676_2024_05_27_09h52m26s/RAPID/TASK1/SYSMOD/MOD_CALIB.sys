MODULE MOD_CALIB(SYSMODULE)

    !Calcul le TCP d'un outil mobile
    PROC Calib_Tool_Mobile_TCP_XZ(INOUT tooldata Tool,robtarget P1,robtarget P2,robtarget P3,robtarget P4,\robtarget PElongZ\robtarget PElongX)
        VAR jointtarget J1;
        VAR jointtarget J2;
        VAR jointtarget J3;
        VAR jointtarget J4;
        VAR jointtarget JElongX;
        VAR jointtarget JElongZ;
        VAR num err_number;
        VAR num err_max;
        VAR num err_mean;

        !Conversion des robtarget en jointtarget
        J1:=CalcJointT(P1,tool0\wobj:=wobj0\ErrorNumber:=err_number);
        J2:=CalcJointT(P2,tool0\wobj:=wobj0\ErrorNumber:=err_number);
        J3:=CalcJointT(P3,tool0\wobj:=wobj0\ErrorNumber:=err_number);
        J4:=CalcJointT(P4,tool0\wobj:=wobj0\ErrorNumber:=err_number);
        IF Present(PElongX) JElongX:=CalcJointT(PElongX,tool0\wobj:=wobj0\ErrorNumber:=err_number);
        IF Present(PElongZ) JElongZ:=CalcJointT(PElongZ,tool0\wobj:=wobj0\ErrorNumber:=err_number);

        !Calcul du TCP
        MToolTCPCalib J1,J2,J3,J4,Tool,err_max,err_mean;

        IF Present(PElongX) THEN

            !Calcul de l'orientation
            MToolRotCalib J4,JElongZ\XPos:=JElongX,Tool;

        ELSEIF Present(PElongZ) THEN

            !Calcul de l'orientation
            MToolRotCalib J4,JElongZ,Tool;
        ELSE
            Tool.tframe.rot:=[1,0,0,0];
        ENDIF
        TPWrite ArgName(Tool);
        TPWrite "Err mean :"+ValToStr(err_mean);
        TPWrite "Err max :"+ValToStr(err_max);

    ENDPROC

    !***************************************************************************************************
    PROC Calib_Tool_Broche_Fixe(INOUT tooldata Tool,robtarget TCP,robtarget X1,robtarget X2,robtarget Y1)
        VAR pose FrameTool;

        !Calcul du frame
        FrameTool:=DefFrame(X1,X2,Y1);

        !Calculs de l'outil
        Tool.tframe.trans:=TCP.trans;
        Tool.tframe.rot:=FrameTool.rot;

    ENDPROC

    PROC Calib_Tool_Motor_Fixe(INOUT tooldata Tool,robtarget Z1,robtarget Z2,robtarget Y1)

        !Calcul du frame
        Tool.tframe:=PoseMult(DefFrame(Z1,Z2,Y1\Origin:=1),[[0,0,0],[0,0.70711,0,0.70711]]);

    ENDPROC

ENDMODULE